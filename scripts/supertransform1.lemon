function bool Character.performSuperTransformation()
{
	if (u8[0xffffe654] == 17 || u8[0xffffe654] == 73)
		return false
	// Already super?
	if (super.active == 0xff || super.active.tails == 0xff)
		return false
	if (!System.getGlobalVariableValueByName("original_mode") && !Input.buttonPressed(BUTTON_Y)) // DA Garden
		return false

	u8 possibleSuperForm = 0	// 1 for Super, 2 for Hyper

	// Tails is first player?
	if (char.character == CHARACTER_TAILS)
	{
		if (!isMainCharacter(CHARACTER_TAILS))
			return false
	}

	// Special handling for Tails
	if (char.character == CHARACTER_TAILS && !Game.getSetting(SETTING_HYPER_TAILS))
	{
		// Ready to get Super Tails?
		if (global.super_emeralds >= 7)
		{
			possibleSuperForm = 2
		}
	}
	else
	{
		// Can go super?
		if (global.super_emeralds >= 7)
		{
			if (super.active || super.active.tails)
				possibleSuperForm = 2
			else
				possibleSuperForm = 1
		}
		else if (global.chaos_emeralds >= 7 && global.traded_emeralds == 0)
			possibleSuperForm = 1
	}

	if (possibleSuperForm == 0)
		return false

	u8 RingRequirement = 50
	if (super.active || super.active.tails)
		RingRequirement = (!HyperRingCost) ? 50 : (HyperRingCost == 1) ? 75 : 100

	if (ring_counter < RingRequirement || !hud.dirty.timer || char.control_flags != 0)	// Last check is needed e.g. for the snowboarding section
		return false

	if (char.character == CHARACTER_SONIC)
	{
		char.mapping_offset = 0x146816
		char.state = char.state.TRANSFORMING

		if (possibleSuperForm == 2)
		{
			super.active = 0xff
			if (HyperSonicStars)
			{
				u32[0xffffcd7c] = 0x019348
				u32[0xffffcbc0] = 0x01a494
			}
		}
		else
		{
			super.active = 1
			if (SuperSonicStars)
				u32[0xffffcbc0] = 0x019156
		}
	}
	else if (char.character == CHARACTER_TAILS)
	{
		super.active.tails = 1
		char.state = char.state.TAILS_TRANSFORM

		if (possibleSuperForm == 2)
		{
			// Call the flicky army of death
			u32[0xffffcd7c] = 0x01a170

		#if STANDALONE
			super.active.tails = 0xff
		#endif
		}
		else
		{
			super.active.tails = 1
			if (SuperTailsStars)
				u32[0xffffcbc0] = 0x019156
		}
	}
	else if (char.character == CHARACTER_KNUCKLES)
	{
		char.state = char.state.TRANSFORMING

		if (possibleSuperForm == 2)
		{
			super.active = 0xff
			if (HyperKnuxStars)
			{
				u32[0xffffcd7c] = 0x019348
				u32[0xffffcbc0] = 0x01a494
			}
			else
				u32[0xffffcbc0] = 0x01a494
		}
		else
		{
			super.active = 1
			if (SuperKnuxStars)
				u32[0xffffcbc0] = 0x019156
		}
	}

#if STANDALONE
	if (super.active == 0xff)
	{
		// Stop drowning music
		if (char.drown_countdown < 12)
			playMusic(MUSIC_CTRL_FADEOUT)

		// Reset drowning countdown
		char.drown_countdown = 30
	}
#endif


	if (super.active == 1 || super.active.tails == 1 || (!Game.getSetting(SETTING_HYPER_TAILS) && super.active.tails == 0xff))
	{
		super.palettefx.state = 1
		super.palettefx.timer = 0x0f
		super.ring_dec.frames = 60
		char.control_flags = 0x81
	}
	playSound(SFX_SUPERTRANSFORM)
#if STANDALONE
	chooseFittingMusic()	// Usually switches to super theme
#else
	playMusic(MUSIC_INVINCIBLE)
#endif

	setSpeedCapProperties(A4)

	char.invinc.countdown = 0
	char.bonus_effect |= char.bonus.INVINCIBLE

#if STANDALONE
	if (super.active == 0xff || super.active.tails == 0xff)
	{
		Game.setAchievementComplete(ACHIEVEMENT_GOING_HYPER)
	}
#endif
	return true
}

// Give Super Tails super stars
//# address-hook(0x0191a4) end(0x019230)
function void fn0191a4()
{

	if (!isMainCharacter(CHARACTER_TAILS))
	{
		base.fn0191a4()
		return
	}

	if (!super.active.tails || super.active.tails == 0xff)
	{
		UnloadObject()
		return
	}

#if STANDALONE
	// Tails charging a Super Peel-Out right now?
	if (u8[0xffffb000 + 0x3d] == 0x80)
	{
		objA0.position.x.u16 += (s16[0xffffb000 + 0x1c] >> 10) * ((u8[0xffffb000 + 0x2a] & char.flag.FACING_LEFT) ? 1 : -1)
	}
#endif

	if (objA0.state != 0)
	{
		SuperSonicStars.CreateSparks()
	}

	if (u8[A0 + 0x34] != 0)
	{
		--objA0.animation.timer
		if (s8(objA0.animation.timer) < 0)
		{
			objA0.animation.timer = 1
			++objA0.animation.sprite
			if (objA0.animation.sprite >= 6)
			{
				objA0.animation.sprite = 0
				u8[A0 + 0x34] = 0
				u8[A0 + 0x35] = 1
				return
			}
		}

		if (u8[A0 + 0x35] == 0)
		{
			objA0.position.x.u16 = u16[0xffffb000 + 0x10]
			objA0.position.y.u16 = u16[0xffffb000 + 0x14]
		}
		DrawObject()
	}
	else
	{
		if (u8[0xffffb000 + 0x2e] == 0 && abs(s16[0xffffb000 + 0x1c]) >= 0x0800)
		{
			objA0.animation.sprite = 0
			u8[A0 + 0x34] = 0x01

			objA0.position.x.u16 = u16[0xffffb000 + 0x10]
			objA0.position.y.u16 = u16[0xffffb000 + 0x14]
			DrawObject()
		}
		else
		{
			u8[A0 + 0x34] = 0
			u8[A0 + 0x35] = 0
		}
	}
}
